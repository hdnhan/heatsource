load "ff-IpOpt"
load "medit"

real T = 1;
int nn = 50;
int Nx = nn, Ny = nn; 
int[int] labs = [1, 2, 0, 4];
mesh Th = square(Nx, Ny, [x, T * y], label = labs);
fespace Vh(Th, P1);


// ***** DATA ***** //
Vh ue = exp(y) * sin(pi * x);
Vh uD = 0;
Vh u0 = ue(x, 0);

Vh fe = sin(pi * x) * sin(pi * y);

Vh q = 2 + x^2 + y^2;
Vh g = (1 + pi^2) * ue - fe * q;
real gamma = 1e-5;



// ** Integrate on space
//   The result is a vector with respect to (t)
func real[int] IntegralOnSpace(real[int] mat){
    real[int] res(Ny + 1);
    Vh temp = 0;
    for (int i = 0; i <= Ny; i++){
        for (int j = 0; j <= Nx; j++){
            temp[][j] = mat[i * (Nx + 1) + j];
        }
        res[i] = int1d(Th, 1)(temp);
    }
    return res;
}

// The integration observation
Vh omg = 1 + x^2 + y^2; // omega
Vh we = omg * ue;
real[int] lue = IntegralOnSpace(we[]);



// ***** Functions to modify matrix ***** //

// ** Change the time direction
func real[int] FlipTimeDirection(real[int] mat){
    Vh res;
    for (int i = 0; i <= Ny/2; i++){
        for (int j = 0; j <= Nx; j++){
            res[][i * (Nx + 1) + j] = mat[(Ny - i) * (Nx + 1) + j];
            res[][(Ny - i) * (Nx + 1) + j] = mat[i * (Nx + 1) + j];
        }
    }
    return res[];
}

// ** To change vec(t) into vec(x, t) depending on the mesh
func real[int] TimeToQ(real[int] mat){
    Vh res = 0;
    for (int i = 0; i <= Ny; i++){
        for (int j = 0; j <= Nx; j++)
        res[][i * (Nx + 1) + j] = mat[i];
    }
    return res[];
}

// ** Inner product on time domain of vec1(t) and vec2(t)
func real InnerProductOnTime(real[int] vec1, real[int] vec2){
    real[int] vec(Ny + 1);
    for (int i = 0; i <= Ny; i++){
        vec[i] = vec1[i] * vec2[i];
    }

    // Take int1d on x = 0 (label = 4);
    Vh res = 0;
    for (int i = 0; i <= Ny; i++){
        res[][i * (Nx + 1)] = vec[i];
    }

    return int1d(Th, 4)(res);
}



// ***** State and Adjoint problems ***** //

// * State problem
func real[int] StateProblem(real[int] ff){
    Vh u, v, f; f[] = ff;
    Vh rsh = q * f + g;

    solve SpaceTime(u, v, solver = sparsesolver) = 
          int2d(Th)(dy(u)  * v + dx(u)  * dx(v)) 
        //+ int2d(Th)(dy(u0) * v + dx(u0) * dx(v))
        - int2d(Th)(rsh * v) 
        + on(2, 4, u = uD) + on(1, u = u0);

    //u = u + u0;
    return u[];
}


// * Adjoint problem
func real[int] AdjointProblem(real[int] uu){
    Vh p, v, u; u[] = uu;
    Vh wh = omg * u;
    real[int] del = IntegralOnSpace(wh[]) - lue;
    Vh delta; delta[] = TimeToQ(del);
    Vh rhs = omg * delta;
    rhs[] = FlipTimeDirection(rhs[]);

    solve SpaceTime(p, v, solver = sparsesolver) =
          int2d(Th)(dy(p) * v + dx(p) * dx(v))
        - int2d(Th)(rhs * v)
        + on(1, 2, 4, p = 0);
    return FlipTimeDirection(p[]);
}


// Tikhonov functional
func real J(real[int] ff) {
    Vh f; f[] = ff;
    Vh u; u[] = StateProblem(ff);
    Vh wh = omg * u; 
    real[int] del = IntegralOnSpace(wh[]) - lue;
    return 0.5 * InnerProductOnTime(del, del) + 0.5 * gamma * int2d(Th)(f^2);
}


// Gradient of Tikhonov funtional
func real[int] GradJ(real[int] ff) {
    Vh p; p[] = AdjointProblem(StateProblem(ff));
    Vh qp = p * q;
    real[int] res = qp[] + gamma * ff;
    return res;
}



Vh fh = 0;
IPOPT(J, GradJ, fh[], tol = 1e-5);

cout << "J: " << J(fh[]) << endl; 
Vh delta = fh - fe;

cout << "fe min and max: " << fe[].min << ", " << fe[].max << endl;
cout << "fh min and max: " << fh[].min << ", " << fh[].max << endl;
cout << "(fh-fe) min and max: " << delta[].min << ", " << delta[].max << endl;
cout << "erorr L2: " << sqrt(int2d(Th)(delta * delta)) << endl;
plot(fe, dim = 3, wait = 1);
plot(fh, dim = 3, wait = 1);
plot(fe, fh, dim = 3, wait = 1);